module main (
    clk     : input  logic   ,
    rst     : input  logic   ,
    key     : input  logic<3>,
    gpio_0_1: input  logic   , // RX
    gpio_0_3: input  logic   , // CTS
    gpio_0_6: input  logic   , // RESB
    gpio_0_0: output logic   , // TX
    gpio_0_2: output logic   , // RTS
    gpio_0_4: output logic   , // RW
    gpio_0_5: output logic   , // Nop(Low if cpumode is not nop)
) {
    let i_clk     : logic    = clk;
    let i_rst     : logic    = rst;
    let _i_key    : logic<3> = key;
    let i_gpio_0_1: logic    = gpio_0_1;
    let i_rx      : logic    = i_gpio_0_1;
    let i_gpio_0_3: logic    = gpio_0_3;
    let i_gpio_0_6: logic    = gpio_0_6;

    let cts           : logic = i_gpio_0_3;
    let resb          : logic = i_gpio_0_6;
    let reset         : logic = i_rst || resb;
    inst if_register   : RegisterIf;
    inst if_memory     : MemoryIf;
    assign if_memory.data = o_ram_data;

    var cpu_mode             : ModePkg::CpuMode     ;
    var o_tmp_cpu_mode       : ModePkg::CpuMode     ;
    var cassette_mode        : ModePkg::CassetteMode;
    let i_mode_handler_en    : logic                 = ~o_mode_handler_finish;
    var o_mode_handler_rts   : logic                ;
    var o_mode_handler_busy  : logic                ;
    var o_mode_handler_finish: logic                ;
    inst mode_handler: ModeHandler (
        i_clk                                 ,
        i_rst          : reset                ,
        i_en           : i_mode_handler_en    ,
        i_rx                                  ,
        o_cpu_mode     : o_tmp_cpu_mode       ,
        o_cassette_mode: cassette_mode        ,
        o_rts          : o_mode_handler_rts   ,
        o_busy         : o_mode_handler_busy  ,
        o_finish       : o_mode_handler_finish,
    );
    assign cpu_mode = if o_debug_finish {
        ModePkg::CpuMode::Nop
    } else {
        o_tmp_cpu_mode
    };

    let i_debug_en            : logic = o_mode_handler_finish && (cpu_mode == ModePkg::CpuMode::DebugWithinMockMemory || cpu_mode == ModePkg::CpuMode::DebugWithinInternalMemory);
    let i_debug_mock_memory_en: logic = cpu_mode == ModePkg::CpuMode::DebugWithinMockMemory;
    let i_debug_tx_en         : logic = ~cts;
    var o_debug_busy          : logic;
    var o_debug_tx            : logic;
    var o_debug_rw            : logic;
    var o_debug_finish        : logic;
    inst if_debug_register     : RegisterIf;
    inst if_debug_memory       : MemoryIf;
    var o_debug_rts           : logic;
    inst debug: Debug (
        i_clk                                    ,
        i_rst                 : reset            ,
        i_en                  : i_debug_en       ,
        i_rx                                     ,
        i_we                  : i_debug_tx_en    ,
        i_debug_mock_memory_en                   ,
        if_register_mst       : if_debug_register,
        if_register_slv       : if_register      ,
        if_memory_mst         : if_debug_memory  ,
        if_memory_slv         : if_memory        ,
        o_tx                  : o_debug_tx       ,
        o_busy                : o_debug_busy     ,
        o_rts                 : o_debug_rts      ,
        o_rw                  : o_debug_rw       ,
        o_finish              : o_debug_finish   ,
    );

    var o_ram_data: logic<8>;
    inst ram: Ram (
        i_clk                                ,
        i_rst                                ,
        i_en           : if_debug_memory.en  ,
        i_we           : if_debug_memory.we  ,
        i_addr         : if_debug_memory.addr,
        i_data         : if_debug_memory.data,
        i_cassette_mode: cassette_mode       ,
        o_data         : o_ram_data          ,
    );

    assign gpio_0_0 = if (cpu_mode == ModePkg::CpuMode::DebugWithinMockMemory || cpu_mode == ModePkg::CpuMode::DebugWithinInternalMemory) {
        if i_debug_tx_en {
            o_debug_tx
        } else {
            1
        }
    } else {
        1
    };
    assign gpio_0_2 = if i_mode_handler_en {
        ~o_mode_handler_busy
    } else if o_debug_busy {
        o_debug_rts
    } else {
        1
    };
    assign gpio_0_4 = if i_debug_en {
        o_debug_rw
    } else {
        1
    };

    assign gpio_0_5 = cpu_mode == ModePkg::CpuMode::Nop;
    always_ff (posedge i_clk, async_high i_rst) {
        if_reset {
            if_register.a  = 0;
            if_register.x  = 0;
            if_register.y  = 0;
            if_register.s  = 0;
            if_register.p  = 0;
            if_register.pc = 0;
        } else {
            if_register.a  = if_debug_register.a;
            if_register.x  = if_debug_register.x;
            if_register.y  = if_debug_register.y;
            if_register.s  = if_debug_register.s;
            if_register.p  = if_debug_register.p;
            if_register.pc = if_debug_register.pc;
        }
    }
}
