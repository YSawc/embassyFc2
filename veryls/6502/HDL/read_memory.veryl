module ReadMemory (
    i_clk             : input  logic   ,
    i_rst             : input  logic   ,
    i_en              : input  logic   ,
    i_we              : input  logic   ,
    i_rx              : input  logic   ,
    i_target_low_addr : input  logic<8>,
    i_target_high_addr: input  logic<8>,
    o_data            : output logic<8>,
    o_tx              : output logic   ,
    o_rts             : output logic   ,
    o_busy            : output logic   ,
    o_finish          : output logic   ,
) {
    var o_rts_next   : logic    ;
    var o_busy_next  : logic    ;
    var o_finish_next: logic    ;
    var status       : Status   ;
    var status_next  : Status   ;
    enum Status: logic <3> {
        Ready,
        LowTx,
        HighTx,
        MemoryRx,
        Finish,
    }

    let i_low_tx_en    : logic    = i_we && status == Status::LowTx;
    let i_low_tx_data  : logic<8> = i_target_low_addr;
    var o_low_tx_tx    : logic   ;
    var o_low_tx_busy  : logic   ;
    var o_low_tx_finish: logic   ;
    inst low_tx: uarty::Tx (
        i_clk                    ,
        i_rst                    ,
        i_en    : i_low_tx_en    ,
        i_data  : i_low_tx_data  ,
        o_tx    : o_low_tx_tx    ,
        o_busy  : o_low_tx_busy  ,
        o_finish: o_low_tx_finish,
    );

    let i_high_tx_en    : logic    = i_we && status == Status::HighTx;
    let i_high_tx_data  : logic<8> = i_target_high_addr;
    var o_high_tx_tx    : logic   ;
    var o_high_tx_busy  : logic   ;
    var o_high_tx_finish: logic   ;
    inst high_tx: uarty::Tx (
        i_clk                     ,
        i_rst                     ,
        i_en    : i_high_tx_en    ,
        i_data  : i_high_tx_data  ,
        o_tx    : o_high_tx_tx    ,
        o_busy  : o_high_tx_busy  ,
        o_finish: o_high_tx_finish,
    );

    let i_memory_rx_en    : logic    = status == Status::MemoryRx;
    var o_memory_rx_data  : logic<8>;
    var o_memory_rx_busy  : logic   ;
    var o_memory_rx_finish: logic   ;
    inst memory_rx: uarty::Rx (
        i_clk                       ,
        i_rst                       ,
        i_rx                        ,
        i_en    : i_memory_rx_en    ,
        o_data  : o_memory_rx_data  ,
        o_busy  : o_memory_rx_busy  ,
        o_finish: o_memory_rx_finish,
    );

    assign o_rts_next = if i_memory_rx_en {
        ~i_memory_rx_en
    } else {
        1
    };
    assign o_data = o_memory_rx_data;
    assign o_tx   = if i_low_tx_en {
        o_low_tx_tx
    } else if i_high_tx_en {
        o_high_tx_tx
    } else {
        1
    };

    always_comb {
        status_next   = status;
        o_finish_next = o_finish;
        case status {
            Status::Ready: {
                if i_en {
                    status_next = Status::LowTx;
                }
            }
            Status::LowTx: {
                if o_low_tx_finish {
                    status_next = Status::HighTx;
                }
            }
            Status::HighTx: {
                if o_high_tx_finish {
                    status_next = Status::MemoryRx;
                }
            }
            Status::MemoryRx: {
                if o_memory_rx_finish {
                    status_next = Status::Finish;
                }
            }
            Status::Finish: {
                status_next = Status::Ready;
            }
        }
        o_finish_next = status_next == Status::Finish;
    }

    always_ff (posedge i_clk, async_high i_rst) {
        if_reset {
            o_busy   = 0;
            o_finish = 0;
            status   = Status::Ready;
            o_rts    = 1;
        } else {
            o_busy   = o_busy_next;
            o_finish = o_finish_next;
            status   = status_next;
            o_rts    = o_rts_next;
        }
    }
}
